#
# Modified version of Sorin theme.
#
# Authors:
#   David Furnes <david@dfurnes.com>
#

# Load dependencies.
pmodload 'helper'

function prompt_davef_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_davef_pwd="$MATCH"
    unset MATCH
  else
    _prompt_davef_pwd="${pwd:t}"
  fi
}

function prompt_davef_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_davef_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_davef_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_davef_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{yellow}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{yellow}❯%f'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❯%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{magenta}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%F{white}$(coalesce "%b" "%p" "%c")%s'

  # Define prompts.
  PROMPT='%F{blue}${_prompt_davef_pwd}%f${git_info+${(e)git_info[prompt]}} ${editor_info[keymap]}${editor_info[overwrite]} %f'
  RPROMPT=''
}

prompt_davef_setup "$@"

